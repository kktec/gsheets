buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3'
    }
}

plugins {
    id 'groovy'
    id 'idea'
    id 'eclipse'
    id 'codenarc'
    id 'com.jfrog.bintray' version '1.3'
    id 'net.saliman.cobertura' version '2.2.8'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'org.gsheets.kktec'
version = '0.4.3-SNAPSHOT'
description = 'A simple Groovy DSL wrapper over Apache POI'

defaultTasks 'clean', 'build', 'cobertura'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'

    compile 'xml-apis:xml-apis:1.0.b2'
    compile 'org.apache.poi:poi:3.8'
    compile 'org.apache.poi:poi-ooxml:3.8'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    testRuntime "org.slf4j:slf4j-log4j12:1.7.5"
    testRuntime "log4j:log4j:1.2.7"
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

//apply plugin: 'codenarc'
codenarc {
    toolVersion = '0.24'
    configFile = file('config/codenarc/rules.groovy')
}

cobertura {
//	coverageSourceDirs = [project.sourceSets.main.groovy.srcDirs]
}

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from sourceSets*.allSource
}

publishing {
    publications {
        gsheets(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
                extension 'jar'
            }
        }
    }
}

bintrayUpload.dependsOn jar, sourceJar


bintray {
    user = bintray_user
    key = bintray_api_key
//    configurations = ['deployables'] // When uploading configuration files
    publications = ['gsheets'] // When uploading Maven-based publication files

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven'
        name = projectName
        desc = projectDescription
        licenses = ['Apache-2.0']
        labels = ['gsheets', 'groovy', 'gradle', 'poi', 'excel']
    }
    dryRun = false // whether to run this as dry-run, without deploying
}


