apply plugin: 'groovy'
sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'org.gsheets.kktec'
version = '0.4.0'
description = 'A simple Groovy DSL wrapper over Apache POI'

defaultTasks 'clean', 'build', 'groovydoc', 'sourceJar'

apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
	jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.0.8'
	
	compile 'org.apache.poi:poi:3.8'
	compile 'org.apache.poi:poi-ooxml:3.8'

	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
	testCompile 'org.hamcrest:hamcrest-library:1.3'
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.11'
}

apply plugin: 'codenarc'
codenarc {
	toolVersion = '0.19'
	configFile = file('config/codenarc/rules.groovy')
}

apply plugin: 'cobertura'
cobertura {
	coverageSourceDirs = [project.sourceSets.main.groovy.srcDirs]
}

apply plugin: 'maven-publish'
apply plugin: 'bintray'


task sourceJar(type: Jar) {
  from sourceSets*.allSource
}

publishing {
	publications {
		gsheets(MavenPublication) {
			from components.java
			
			artifact sourceJar {
				classifier 'sources'
				extension 'jar'
			}
		}
	}
}

buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
	
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
		classpath "net.saliman:gradle-cobertura-plugin:2.0.0"
	}
}


bintrayUpload.dependsOn jar, sourceJar


bintray {
	user = bintray_user
	key = bintray_api_key
//    configurations = ['deployables'] // When uploading configuration files
	publications = ['gsheets'] // When uploading Maven-based publication files
	
	def projectName = project.name
	def projectDescription = project.description

	pkg {
		repo = 'maven'
		name = projectName
		desc = projectDescription
		licenses = ['Apache-2.0']
		labels = ['gsheets', 'groovy', 'gradle', 'poi', 'excel']
	}
	dryRun = false // whether to run this as dry-run, without deploying
}

